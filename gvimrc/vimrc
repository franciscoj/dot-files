set shell=zsh
set nocompatible

filetype off " Asked by Vundle

set rtp+=~/.vim/bundle/vundle/
call vundle#rc()

Bundle 'vim-ruby/vim-ruby'
Bundle 'tpope/vim-rails'
Bundle 'tpope/vim-rake'
Bundle 'Textile-for-VIM'
Bundle 'tpope/vim-cucumber'
Bundle 'tpope/vim-haml'
Bundle 'tpope/vim-markdown'
Bundle 'groenewege/vim-less'
Bundle 'pangloss/vim-javascript'
Bundle 'kchmck/vim-coffee-script'
Bundle 'bronson/vim-ruby-block-conv'
Bundle 'andersjanmyr/nginx-vim-syntax'
Bundle 'puppetlabs/puppet-syntax-vim'
Bundle 'tangledhelix/vim-octopress'
Bundle 'altercation/vim-colors-solarized'

Bundle 'gmarik/vundle'
Bundle 'mileszs/ack.vim'
Bundle 'tpope/vim-git'
Bundle 'tpope/vim-fugitive'

Bundle 'tpope/vim-surround'
Bundle 'tpope/vim-ragtag'
Bundle 'tpope/vim-commentary'
Bundle 'tpope/vim-repeat'
Bundle 'edsono/vim-matchit'
Bundle 'tpope/vim-endwise'
Bundle 'tpope/vim-unimpaired'
Bundle 'Raimondi/delimitMate'
Bundle 'godlygeek/tabular'
Bundle 'textobj-user'
Bundle 'nelstrom/vim-textobj-rubyblock'
Bundle 'scrooloose/syntastic'
Bundle 'nathanaelkane/vim-indent-guides'
Bundle 'vim-scripts/YankRing.vim'
Bundle 'scrooloose/nerdtree'
Bundle 'kien/ctrlp.vim'

Bundle 'xolox/vim-easytags'
Bundle 'tpope/vim-bundler'

Bundle 'jgdavey/tslime.vim'
Bundle 'jgdavey/vim-turbux'
Bundle 'Lokaltog/vim-powerline'
Bundle 'tpope/vim-eunuch'

" ******************************************************************************
" Make it pretty
" ******************************************************************************
if !has('gui_running')
  set background=light
  let g:solarized_termtrans = 1
  set guioptions=ac
endif

colorscheme solarized

set number
set cursorline
set colorcolumn=80
set list
set listchars=tab:>\ ,eol:¬,trail:·
set diffopt+=iwhite
set cpoptions+=$ " Mark with a $ at the end of a 'change' command
set showcmd
set laststatus=2 " Always show status line.

" Filetype highlighting and indentation activation
syntax on
filetype on
filetype plugin on
filetype indent on

" Pretty indexing with 2 spaces
set tabstop=2
set shiftwidth=2
set softtabstop=2
set expandtab

" Search config
set hlsearch " Auto highlight search
set incsearch " Incremental search
set ignorecase " Ignore case during search
set smartcase " Be smart when matching case on the search

" Default folding config
set foldmethod=indent
set foldnestmax=10
set nofoldenable
set foldlevel=1

" Others
set scrolloff=3 " Leave 3 lines before start scrolling
set backspace=indent,eol,start " Better when removing lines
" English spelling by default
set spell
set spelllang=en_us

" Auto complete options
set completeopt=longest,menu
set complete=.,w,b,u,t
set wildmode=longest,list:longest

set pastetoggle=<F3>
set showmode

runtime macros/matchit.vim " Needed for the ruby block object plugin

" Filetype config for some missing files on vim-ruby
autocmd BufRead,BufNewFile {caprc,autotest,.caprc} set ft=ruby
" Filetype config for nginx conf files
autocmd BufRead,BufNewFile /opt/nginx/conf/* set ft=nginx
" Filetype config for octopress files
autocmd BufRead,BufNewFile /projects/octopress/source/_posts/*.markdown set ft=octopress

" Set auto completion for ruby files
autocmd FileType ruby,eruby set omnifunc=rubycomplete#Complete
autocmd FileType ruby,eruby let g:rubycomplete_buffer_loading = 1
autocmd FileType ruby,eruby let g:rubycomplete_rails = 1
autocmd FileType ruby,eruby let g:rubycomplete_classes_in_global = 1

" Configuration for used plugins
let NERDTreeDirArrows=1
" Fixes incompatibility with NERDTree and vim-bundler
let g:NERDTreeHijackNetrw = 0
" Sintastic.vim config
let g:syntastic_enable_signs=1
" Force easytags.vim to take ctags from homebrew
let g:easytags_cmd = "/usr/local/bin/ctags"
" Use solarized to highlight trailing white spaces
let g:solarized_hitrail = 1
" Don't mess with yankring
let g:yankring_manual_clipboard_check = 1
" Powerline config
let g:Powerline_symbols = 'unicode'
" ctrlp mapping
let g:ctrlp_map = 'ff'
" Some extras to rails.vim "{{{
autocmd User Rails Rnavcommand steps features/step_definitions -glob=**/*

let mapleader=","

" Misc mappings
imap jj <ESC>

map ,f mmgg=G`m^
map <silent> <F2> :FixWhitespace<CR>
map <silent> ,n :noh<CR>
map ,e :e <C-R>=expand("%:p:h") . "/" <CR>
map ,te :tabe <C-R>=expand("%:p:h") . "/" <CR>

nnoremap <space> za
nnoremap <silent> ,<space> :NERDTreeToggle<CR>
noremap ,a :Ack<space>
nnoremap <F3> :set invpaste paste?<CR>

nnoremap <c-j> <c-w>j
nnoremap <c-k> <c-w>k
nnoremap <c-h> <c-w>h
nnoremap <c-l> <c-w>l
nnoremap <silent> ,cc :close<CR>

nmap Q gqq
nmap <silent> ,,ev :e $MYVIMRC
nmap <silent> ,,sv :so $MYVIMRC
nmap <silent> ,gs :Gstatus<CR>
nmap <silent> ,gd :Gdiff<CR>
" Tslime
nmap <C-c>r <Plug>SetTmuxVars

nmap <silent> ,a= :Tabularize /=<CR>
vmap <silent> ,a= :Tabularize /=<CR>
nmap <silent> ,a: :Tabularize /:<CR>
vmap <silent> ,a: :Tabularize /:<CR>
nmap <silent> ,a:: :Tabularize /:\zs<CR>
vmap <silent> ,a:: :Tabularize /:\zs<CR>
nmap <silent> ,a, :Tabularize /,<CR>
vmap <silent> ,a, :Tabularize /,<CR>
nmap <silent> ,a<Bar> :Tabularize /<Bar><CR>
vmap <silent> ,a<Bar> :Tabularize /<Bar><CR>


" Align Cucumber tables with Tabular plugin "{{{
inoremap <silent> <Bar>   <Bar><Esc>:call <SID>align()<CR>a
function! s:align()
  let p = '^\s*|\s.*\s|\s*$'
  if exists(':Tabularize') && getline('.') =~# '^\s*|' && (getline(line('.')-1) =~# p || getline(line('.')+1) =~# p)
    let column = strlen(substitute(getline('.')[0:col('.')],'[^|]','','g'))
    let position = strlen(matchstr(getline('.')[0:col('.')],'.*|\s*\zs.*'))
    Tabularize/|/l1
    normal! 0
    call search(repeat('[^|]*|',column).'\s\{-\}'.repeat('.',position),'ce',line('.'))
  endif
endfunction
"}}}

" ******************************************************************************
" Misc functions
" ******************************************************************************
" Set backup directories"{{{
function! InitializeDirectories()
  let separator = "."
  let parent = $HOME
  let prefix = '.vim/'
  let dir_list = {
        \ 'backup': 'backupdir',
        \ 'views': 'viewdir',
        \ 'swap': 'directory' }

  for [dirname, settingname] in items(dir_list)
    let directory = parent . '/' . prefix . dirname . "/"
    if exists("*mkdir")
      if !isdirectory(directory)
        call mkdir(directory)
      endif
    endif
    if !isdirectory(directory)
      echo "Warning: Unable to create backup directory: " . directory
      echo "Try: mkdir -p " . directory
    else
      let directory = substitute(directory, " ", "\\\\ ", "")
      exec "set " . settingname . "=" . directory
    endif
  endfor
endfunction
call InitializeDirectories()"}}}

" Removes all traililing whitespaces from the buffer
function! s:FixWhitespace(line1,line2)
    let l:save_cursor = getpos(".")
    silent! execute ':' . a:line1 . ',' . a:line2 . 's/\s\+$//'
    call setpos('.', l:save_cursor)
endfunction
" Run :FixWhitespace to remove end of line white space.
command! -range=% FixWhitespace call <SID>FixWhitespace(<line1>,<line2>)

function! s:PrettifyXml()
    let l:save_cursor = getpos(".")
    silent! execute ':%s/\n//'
    silent! execute ':1,$!xmllint --format --recover - 2>/dev/null'
    silent! execute "normal 2Gdd"
    silent! execute ':w'
    call setpos('.', l:save_cursor)
endfunction
command! PrettifyXml call <SID>PrettifyXml()
